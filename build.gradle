plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'org.springdoc') {
                details.useVersion '2.8.0'
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring starters
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'

    // DB driver
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

    // ✅ Swagger / OpenAPI (Spring Boot 3.4.x 라인)
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.0'

    // JWT (jjwt 0.11.5)
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // dotenv
    implementation 'io.github.cdimascio:java-dotenv:5.2.2'

    // MapStruct
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    // AWS S3
    implementation platform("software.amazon.awssdk:bom:2.41.1")
    implementation 'software.amazon.awssdk:s3'

    // Firebase
    implementation('com.google.firebase:firebase-admin:9.2.0') {
        exclude group: 'com.google.guava', module: 'guava'
    }
    implementation 'com.google.guava:guava:32.1.3-jre'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Redisson
    implementation 'org.redisson:redisson-spring-boot-starter:4.1.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.9.25"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.9.25"

    // Tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}
